apply plugin: 'com.android.application'

def versionMajor = 1;
def versionMinor = 0;
def versionPatch = 0;
def versionBuild = 0;


def getDate() {
    def date = new Date()
    def formattedDate = date.format('MMdd')
    return formattedDate
};


//设置编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"
    // 默认的文件配置
    defaultConfig {
        applicationId "com.android.gradlecourse"
        minSdkVersion 15
        targetSdkVersion 22
        // 版本名称
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
        // 版本号
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch*10+versionBuild
    }
    // 构建时,忽略lint检查
    lintOptions {
        abortOnError false
    }
    //为不同的buildTypes配置签名
    signingConfigs {
        //release的keystore信息
        release {
            storeFile file("baofeng.keystore")
            storePassword "Xr780o22Qa"
            keyAlias "baofeng"
            keyPassword "Xr780o22Qa"
        }
        debug {
        }
    }
    buildTypes {
        release {
            // 文件对齐压缩
            zipAlignEnabled true
            // 移除未使用的资源
            shrinkResources true
            // 调用签名文件
            signingConfig signingConfigs.release
            // 是否使用混淆文件
            minifyEnabled true
            // 指定混淆文件的路径
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 为该类型设置属性值
            buildConfigField "boolean", "DEBUG_MODEL", "false"
            // 字符串类型
            buildConfigField "String", "FOO", "\"bar\""
            // 增加资源文件的属性值
            resValue "string", "foo", "foosss2"
        }
        Test {
        }
        debug {
            debuggable true
            jniDebuggable false
            signingConfig signingConfigs.debug
            renderscriptDebuggable false
            renderscriptOptimLevel 3
            minifyEnabled false
            pseudoLocalesEnabled true
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
            zipAlignEnabled false
        }
    }
    dexOptions {
        // 是否进行多dex打包,可以解决6万方法问题
        incremental true
    }
    compileOptions {
        // 配置jdk版本
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
    }
}

dependencies {
    // 依赖libs下面的jar
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // 远程依赖 V7
    compile 'com.android.support:appcompat-v7:22.0.0'
}
